{

    inputs = {

        nixpkgs.url =
            "https://channels.nixos.org/nixos-unstable-small/nixexprs.tar.zst";

        # Some modules

        preservation.url = "github:nix-community/preservation";

        sops-nix = {
            url = "github:Mic92/sops-nix";
            inputs.nixpkgs.follows = "nixpkgs";
        };

        disko = {
            url = "github:nix-community/disko";
            inputs.nixpkgs.follows = "nixpkgs";
        };

        xremap = {
            url = "github:xremap/nix-flake";
            inputs.nixpkgs.follows = "nixpkgs";
            inputs.flake-parts.follows = "flake-parts";
        };

        # Some packages
        # (llm-agents is pinned by store path instead — see packages/__pin/)

        # Pinned: firefox 155.0.1
        nixpkgs-firefox.url =
            "github:NixOS/nixpkgs/8f8805619079a90816b72377d83519e87b61565d";

        # tangled = {
        #     url = "git+https://tangled.org/tangled.org/core?shallow=1";
        #     inputs = {
        #         nixpkgs.follows = "nixpkgs";
        #         flake-compat.follows = "empty";
        #         gomod2nix.inputs.flake-utils.follows = "flake-utils";
        #         indigo.follows = "empty";
        #         htmx-src.follows = "empty";
        #         htmx-ws-src.follows = "empty";
        #         lucide-src.follows = "empty";
        #         inter-fonts-src.follows = "empty";
        #         actor-typeahead-src.follows = "empty";
        #         ibm-plex-mono-src.follows = "empty";
        #     };
        # };

        noctalia = {
            url = "github:noctalia-dev/noctalia-shell/v5.0.1";
            inputs.nixpkgs.follows = "nixpkgs";
        };

        # Some toolchains

        rust-overlay = {
            url = "github:oxalica/rust-overlay";
            inputs.nixpkgs.follows = "nixpkgs";
        };

        # Follows

        flake-utils = {
            url = "github:numtide/flake-utils";
        };

        flake-parts = {
            url = "github:hercules-ci/flake-parts";
        };
    };

    outputs = { self, nixpkgs, ... } @ flakes: let

        lib = nixpkgs.lib.extend ( import ./tsukilib );

        pkgsBrew = lib.brewNixpkgs nixpkgs {
            config = { allowUnfree = true; };
            overlays = [
                self.overlays.nuclage
            ];
        };

    in rec {

        /*
         * My cute lib
         */

        inherit lib flakes;

        /*
         * Overlays & packages
         */

        overlays.nuclage =
            import ./packages { inherit lib flakes; };

        inherit pkgsBrew;

        packages = self.pkgsBrew lib.id;

        /*
         * Machines
         */

        nixosConfigurations = let
            modules =
                with flakes; [
                    sops-nix.nixosModules.default
                    preservation.nixosModules.default
                    ./lore/module.nix
                ]
                ++ (lib.listAllModules ./nixos)
                ++ (lib.listAllModules ./sops);
            nixos = lib.brewOS {
                inherit pkgsBrew modules;
                arguments = { inherit flakes; };
            };
        in {
            ren = nixos "x86_64-linux" <| (
                lib.listAllModules ./machine/ren
                ++ (with flakes; [
                    xremap.nixosModules.default
                    noctalia.nixosModules.default
                ]));
            # phia = nixos "x86_64-linux" <| lib.listAllModules ./machine/phia;
            uk-01 = nixos "x86_64-linux" <| (
                lib.listAllModules ./machine/uk-01
                ++ [ flakes.disko.nixosModules.default ]
            );
        };

        colmenaHive =
            {
                meta.nixpkgs = pkgsBrew.pkgsOf "x86_64-linux";
                ren.deployment = {
                    targetHost = "ren.local";
                    buildOnTarget = true;
                };
                phia.deployment = {
                    targetHost = "phia.local";
                    targetUser = "root";
                };
                uk-01.deployment = {
                    targetHost = "uk-01";
                    targetUser = "root";
                };
            }
            |> lib.nixos2colmena self.nixosConfigurations
            |> lib.flip removeAttrs [ "ren" ]
            |> flakes.colmena.lib.makeHive;
    };

}

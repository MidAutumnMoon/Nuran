{
    lib,
    stdenv,
    tsuki,
    autoPatchelfHook,

    libbsd,
    alsa-lib,
    libxkbcommon,
    wayland,
    vulkan-loader,
    zlib,
    glibc,
    glib,
    libxcb,
    libX11,

    makeBinaryWrapper,
    nodejs,

    mimalloc,
}:

# Based on
# - zed-industries/zed : build.nix
# - nixpkgs zed
# - HPsaucii/zed-editor-flake
stdenv.mkDerivation rec {

    pname = "zed";
    version = "1.18.1";

    src = tsuki.fetchGitHubRelease {
        owner = "zed-industries";
        repo = "zed";
        tag = "v${version}";
        file = "zed-linux-x86_64.tar.gz";
        hash = "sha256-7qYiaNjsX9NYffBvp24HLBBMyl4LCwq+y8KK5bh8C60=";
    };

    nativeBuildInputs = [
        autoPatchelfHook
        makeBinaryWrapper
    ];

    buildInputs = [
        stdenv.cc.libc
        stdenv.cc.cc
        libbsd
        alsa-lib
        libxkbcommon
        wayland
        libX11
        libxcb
        zlib
        glib
    ];

    runtimeDependencies = [
        glibc
        vulkan-loader
        wayland
    ];

    installPhase = ''
        mkdir -pv "$out"
        # remove bundled libs
        rm -r "lib"
        rm *.md
        mv -t "$out" *
        # Keep the named icon available to loaders that use the unthemed fallback.
        mkdir -p "$out/share/pixmaps"
        ln -s ../icons/hicolor/512x512/apps/zed.png "$out/share/pixmaps/zed.png"
        addAutoPatchelfSearchPath "$out/libexec"
    '';

    postFixup = ''
        for f in "$out/bin/zed" "$out/libexec/zed-editor"
        do
            wrapProgram "$f" \
                --inherit-argv0 \
                --suffix PATH : "${lib.makeBinPath [ nodejs ]}" \
                --set LD_PRELOAD "${mimalloc}/lib/libmimalloc.so"
        done
    '';

    meta = {
        description = "Binary Zed Editor";
        mainProgram = "zed";
    };
}
